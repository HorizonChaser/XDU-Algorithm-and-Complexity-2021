// Using online simulator from
// https://turingmachinesimulator.com/

// Algorithm:
//    0. add delimiters '#' on the both sides of the input
//    1. overwrite the 1 to 'a' currently under the head, and move to the right '#', overwrite it to 'b',
//        and write '#' on the next right cell;
//        if 'b' is under the head, it means the calculation is now finished
//    2. move to the right and stop at the first 'a' we meet, move right and continue to step 1
//    3. when it's finished, move to the leftmost '#', delete it and move to the right, overwrite any 'a' and 'b' to 1,
//       when meet the rightmost '#', delete it, re-position the head and then halt

// Test Cases:
//    [1] 111    =>    111111
//    [2] 1    =>    11
 
name: Multiplication by 2 in unary system
init: start0
accept: halt

start0, 1
startl, 1, <

startl, _
startr, #, >

startr, 1
startr, 1, >

startr, _
mvstart, #, <

mvstart, 1
mvstart, 1, <

mvstart, #
start, #, -

start, #
rpa, #, >

rpa, 1
mvrt, a, >

rpa, b
fini_r, b, >

mvrt, 1
mvrt, 1, >

mvrt, b
mvrt, b, >

mvrt, #
nlim, b, >

nlim, _
mvl, #, <

mvl, 1
mvl, 1, <

mvl, b
mvl, b, <

mvl, a
rpa, a, >

fini_r, #
rpab, #, <

fini_r, a
fini_r, a, >

fini_r, b
fini_r, b, >

rpab, b
rpab, 1, <

rpab, a
rpab, 1, <

rpab, #
halt0, _, >

halt0, 1
halt0, 1, >

halt0, #
halt, _, <